plugins {
    id 'application' 
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id 'org.beryx.jlink'
}

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation project(':core')
}

application {
    mainClass = 'org.cirdles.ambapo.userInterface.AmbapoLauncher' 
    mainModule = 'AmbapoApp'
}

javafx {
    version = '17'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

ext.os = org.gradle.internal.os.OperatingSystem.current()
jlink {
    imageZip = file("$buildDir/image-zip/Ambapo.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
 
    launcher {
        name = 'Ambapo'
        noConsole = true
    }
    
    jpackage {
        if(os.windows) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
            imageOptions += ['--icon', "src/main/java/logo/windows/ambapoLogo.ico"]
        }
        else if(os.linux) {
            installerOptions += ['--linux-package-name', 'Ambapo','--linux-shortcut']
            imageOptions += ['--icon', "src/main/java/logo/linux/ambapoLogo.png"]
        }
        else if(os.macOsX) {
            installerOptions += ['--mac-package-name', 'Ambapo']
            imageOptions += ['--icon', "src/main/java/logo/macosx/ambapoLogo.icns"]
        }
    }
}
